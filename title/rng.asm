; lookup tables of RNG values for different framerules, each offset is another 2100 frames
resume_0: .byte $2e, $cb, $25, $e2, $a0, $7f, $a2, $4f, $51, $e6, $6f, $cf, $41, $9c, $78, $75, $4d, $58, $3e, $5e, $47, $0b, $fe, $7d, $b4, $3b, $7a, $bd, $d2, $d5, $c0, $6f, $fd, $35, $4e, $e9, $0e, $4a, $2e, $0b, $c4, $bc, $0b, $eb, $28, $06, $12, $7a, $dc, $04, $18, $48, $ca, $ea, $63, $dd, $59, $7c, $96, $83, $91, $72, $14, $f9, $a9, $5c, $15, $c4, $bd, $ad, $09, $f5, $e9, $40, $08, $ee, $7a, $33, $71, $4b, $1a, $0c, $ba, $3c, $fc, $51, $01, $af, $f1, $52, $73, $94, $9c, $f6, $d6, $d3, $82, $5d, $19
resume_1: .byte $f4, $68, $83, $b5, $b9, $53, $27, $e3, $cd, $23, $6b, $2c, $8f, $55, $34, $3f, $f3, $fa, $9c, $36, $37, $8d, $29, $c9, $93, $07, $fa, $da, $c1, $9a, $bd, $f7, $b8, $65, $6c, $58, $f6, $0a, $74, $e1, $43, $4b, $4b, $e2, $60, $9e, $fc, $e4, $c5, $6c, $26, $8c, $dd, $2f, $b3, $2c, $99, $ea, $a1, $70, $c2, $6c, $dc, $ac, $3c, $54, $75, $1d, $10, $06, $21, $a8, $ec, $48, $8c, $1d, $ce, $ed, $87, $0d, $60, $e8, $33, $5e, $df, $17, $f5, $e8, $e6, $ea, $33, $95, $61, $03, $79, $64, $27, $83, $bd
resume_2: .byte $a9, $fe, $c8, $70, $f8, $ad, $63, $7c, $6e, $ef, $b5, $b2, $0c, $6d, $c4, $d5, $68, $4b, $e1, $8a, $b9, $9a, $d5, $32, $fa, $71, $0f, $a1, $64, $31, $3c, $28, $43, $0f, $f0, $8a, $eb, $9e, $28, $f6, $cb, $33, $5d, $35, $30, $93, $d9, $11, $7c, $64, $16, $1d, $48, $fb, $74, $96, $2a, $13, $8c, $76, $e1, $88, $f5, $5f, $6e, $ec, $5f, $95, $6a, $5c, $33, $43, $3f, $c8, $9d, $c1, $3b, $8a, $64, $9b, $54, $f1, $47, $26, $26, $b5, $f6, $b7, $05, $4f, $d5, $bc, $59, $ef, $d5, $c2, $23, $38, $8e
resume_3: .byte $40, $2f, $cf, $1a, $8b, $0a, $2d, $ba, $f4, $a8, $62, $eb, $12, $c7, $ad, $aa, $8e, $bf, $d8, $e7, $d6, $81, $86, $a0, $dd, $7f, $fb, $14, $e6, $05, $46, $c6, $33, $c5, $29, $3b, $06, $8b, $c0, $35, $4c, $a5, $cb, $f1, $f0, $ae, $20, $d9, $f6, $bc, $5a, $05, $f2, $a4, $12, $cf, $18, $c7, $cf, $96, $64, $51, $4c, $07, $16, $45, $b5, $ae, $4a, $50, $71, $13, $e7, $59, $84, $fa, $a7, $51, $6a, $80, $94, $20, $21, $9a, $98, $9a, $1d, $66, $c9, $9b, $b2, $97, $9b, $e8, $26, $0b, $6d, $3e, $f5
resume_4: .byte $00, $d3, $5e, $fa, $7a, $50, $eb, $43, $29, $77, $08, $8e, $0a, $1c, $24, $01, $5f, $28, $1b, $f2, $a5, $b4, $2d, $c5, $28, $9d, $e4, $56, $2f, $67, $3e, $97, $b5, $da, $c9, $2f, $d0, $b6, $91, $d9, $da, $c2, $70, $9a, $91, $89, $92, $fa, $0f, $75, $76, $3f, $63, $53, $fa, $e2, $4c, $e0, $d6, $7b, $a6, $41, $a6, $b9, $ca, $9d, $0a, $85, $9e, $e8, $17, $95, $98, $c9, $bf, $79, $d0, $45, $a2, $b7, $3d, $c2, $af, $d7, $d5, $f1, $f1, $08, $c2, $04, $19, $ee, $28, $37, $8c, $8f, $2b, $4e, $e8
resume_5: .byte $00, $8c, $c0, $cf, $6c, $44, $b0, $37, $c1, $27, $cc, $59, $2e, $92, $7e, $55, $43, $56, $ab, $3d, $08, $b7, $21, $84, $92, $62, $13, $7e, $e3, $6d, $b2, $1a, $d2, $51, $9a, $59, $dd, $a1, $10, $b2, $43, $89, $e6, $79, $70, $d4, $d3, $49, $e3, $0d, $c2, $35, $87, $1b, $df, $7d, $7c, $6f, $49, $57, $6f, $e3, $3f, $b6, $e7, $16, $60, $d8, $0b, $49, $f5, $b2, $57, $7a, $b6, $8d, $9f, $e7, $77, $b6, $15, $83, $ec, $e2, $e4, $c4, $ca, $c4, $51, $32, $7d, $c1, $1e, $e7, $c1, $98, $f1, $32, $03
resume_6: .byte $00, $2b, $7d, $3a, $98, $e4, $67, $b1, $92, $c9, $dd, $45, $3a, $ab, $36, $57, $fd, $06, $9c, $d9, $42, $de, $7b, $0f, $c3, $58, $db, $d2, $bc, $a3, $cf, $34, $b9, $e5, $09, $07, $7c, $cc, $32, $01, $f7, $0c, $07, $4d, $52, $c7, $f6, $bd, $fc, $e7, $2f, $4b, $40, $bd, $2a, $b9, $e4, $af, $e5, $a1, $23, $60, $73, $c5, $72, $2c, $74, $d3, $37, $99, $da, $99, $67, $e8, $c9, $7e, $3e, $6c, $33, $d9, $6f, $06, $b3, $4d, $4f, $27, $29, $d5, $d5, $3a, $4f, $1c, $4e, $88, $d8, $87, $a6, $ae, $d3
; ================================================================
;  Setup framerule RNG
; ----------------------------------------------------------------
RNGQuickResume:
    lda MathFrameruleDigitStart+3        ; get hundreds and thousands digits of bcd framerule value
    jsr MultiplyBy10                     ;
    adc MathFrameruleDigitStart+2        ;
    tax                                  ; store in X
    lda resume_0,x                       ; get rng value from lookup table for this offset
    sta PseudoRandomBitReg+0             ;
    lda resume_1,x                       ;
    sta PseudoRandomBitReg+1             ;
    lda resume_2,x                       ;
    sta PseudoRandomBitReg+2             ;
    lda resume_3,x                       ;
    sta PseudoRandomBitReg+3             ;
    lda resume_4,x                       ;
    sta PseudoRandomBitReg+4             ;
    lda resume_5,x                       ;
    sta PseudoRandomBitReg+5             ;
    lda resume_6,x                       ;
    sta PseudoRandomBitReg+6             ;
@FrameruleAdjust:
    lda MathFrameruleDigitStart + 1      ; get ones and tens digits of bcd framerule value
    jsr MultiplyBy10                     ;
    adc MathFrameruleDigitStart + 0      ;
    tay                                  ; store in Y
    beq @FrameAdjust                     ; if not set, we can skip ahead
    jsr FRStepRNGByY                     ; otherwise we advance the RNG based on the framerule value * 18
@FrameAdjust:
    ldx SettablesPUP                     ; check if player has selected a powerup
    bne :+                               ; yes - skip ahead to adjust rng
    rts                                  ; no - we are done!
:   ldy @FramerulePowerupAdjust-1,x      ; check rng steps for current powerup value
    jsr StepRNGByY                       ; and adjust the rng for this value
    dex                                  ; decrement to next powerup value
    bne :-                               ; and loop until we reach small mario state
    rts                                  ; then we are done!
@FramerulePowerupAdjust:
    .byte 59                            ; fr pause from grabbing mushroom
    .byte 63                            ; fr pause from grabbing fireflower
    .byte 254 - 63                      ; fr pause from small fire (minus fr pause from grabbing fire)
    .byte 59                            ; fr pause from grabbing mushroom
    .byte 63                            ; fr pause from grabbing fireflower
; ================================================================

; ================================================================
;  Advance RNG by Y framerules
; ----------------------------------------------------------------
FRStepRNGByY:
    jsr FRStepRNG
    dey
    bne FRStepRNGByY
    rts
; ================================================================

; ================================================================
;  Advance RNG by Y frames
; ----------------------------------------------------------------
StepRNGByY:
    jsr SingleStepRNG
    dey
    bne StepRNGByY
    rts
; ================================================================

; ================================================================
;  Advance RNG by 18 frames
; ----------------------------------------------------------------
FRStepRNG:
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jmp SingleStepRNG
; ================================================================

; ================================================================
;  Advance RNG by a single game frame
; ----------------------------------------------------------------
SingleStepRNG:
    pha
    lda PseudoRandomBitReg         ; get first memory location of LSFR bytes
    and #%00000010                 ; mask out all but d1
    sta $00                        ; save here
    lda PseudoRandomBitReg+1       ; get second memory location
    and #%00000010                 ; mask out all but d1
    eor $00                        ; perform exclusive-OR on d1 from first and second bytes
    clc                            ; if neither or both are set, carry will be clear
    beq RotPRandomBit
    sec                            ; if one or the other is set, carry will be set
RotPRandomBit:
    ror PseudoRandomBitReg+0       ; rotate carry into d7, and rotate last bit into carry
    ror PseudoRandomBitReg+1       ; rotate carry into d7, and rotate last bit into carry
    ror PseudoRandomBitReg+2       ; rotate carry into d7, and rotate last bit into carry
    ror PseudoRandomBitReg+3       ; rotate carry into d7, and rotate last bit into carry
    ror PseudoRandomBitReg+4       ; rotate carry into d7, and rotate last bit into carry
    ror PseudoRandomBitReg+5       ; rotate carry into d7, and rotate last bit into carry
    ror PseudoRandomBitReg+6       ; rotate carry into d7, and rotate last bit into carry
    pla
    rts
; ================================================================
